desc: Simple Smooth Gain
// version: 1.0
// author: binbinhfr
// about:
//   A simple plugin that adds or removes gain to the audio signal, 
//   but changing the resulting gain smoothly if the target value changes rapidly.
//   The "smoothness" of the effect can be adjusted
// changelog:
//   v1.0 initial release

slider1:gain_db=0<-32,24,0.01>gain target (dB)
slider2:prog=10<0,100,0.1>smooth (0:immediate,1-100)
slider3:0<-32,24,0.01>momentary gain (dB)

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init
gain_next=10^(gain_db/20);
gain_cur=gain_next;
gain_out=gain_db;

@slider
gain_next=10^(gain_db/20);
time_per_block = samplesblock/srate;
ratio=(prog == 0) ? 1 : time_per_block*10/prog;

@block
// progressive change
gain_diff = gain_next - gain_cur;

(abs(gain_diff) > 0.0001 & prog != 0) ?
(
  gain_delta = gain_diff*ratio;
  gain_cur += gain_delta;
) :
(
  gain_cur = gain_next;
);

(gain_diff != 0) ? 
(
  gain_out = 20*(log(gain_cur)/log(10));
  gain_out = 0.01*ceil(gain_out*100-0.5);
);

slider3 = gain_out;

@sample
spl0 *= gain_cur;
spl1 *= gain_cur;


