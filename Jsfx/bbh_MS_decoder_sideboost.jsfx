 desc:MS Decoder with boosted side
// version: 1.1
// author: binbinhfr 
// about: 
//   Change the gain of the side channel while MS encoding/decoding, to emphasize/reequilibrate the effect 
//   on the side channel of any plugin applied to the signal (use the plugin in LR mode !).
//   Adapted from an original idea of Dan Worall (all credits to him !)
//   https://www.youtube.com/watch?v=dD6_Bajj2DI
//   Choose an ID common to a unique encoder/decoder chain, to link both instances together and
//   share the side boost value in realtime.
//   Default side boost value is +6dB (as advised by DW). For classical MS encoding, use 0dB.
// changelog:
//   v1.0 initial release
//   v1.1 disable encoding or decoding if a plugin with a corresponding ID is not found.

in_pin:mid input
in_pin:side input
out_pin:left output
out_pin:right output

options:gmem=BbhMSEncoderDecoder

slider1:coder_id=1<1,99,1>Encoder/decoder ID
slider2:gain_db=6.0<-32,32,0.01>Side signal boost (dB) (readonly)
slider3:encoder_present=0<0,1,1{no,yes}>Encoder present ?

//-----------------------------------------------------------------------------------
@init
check_count = 0;
check_max = 10;

//-----------------------------------------------------------------------------------
@block
gain_db = gmem[coder_id*2];
gain = 10^(gain_db/20);

(check_count += 1) > check_max ? (
  gmem[coder_id*2+1] == 2 ? encoder_present = 0;
);
gmem[coder_id*2+1] != 2 ? (
  gmem[coder_id*2+1] = 2;
  check_count = 0;
  encoder_present = 1;
);

status = gmem[coder_id*2+1];

//-----------------------------------------------------------------------------------
@sample
encoder_present == 1 ? (
  left = spl0 + spl1 / gain;
  right = spl0 - spl1 / gain;
  spl0 = left;
  spl1 = right;
);
