desc:MS Encoder with separate encoding gains
// version: 1.2
// author: binbinhfr 
// about: 
//   Change the gain of the mid and side channel while MS encoding/decoding, 
//   to emphasize or diminish the effect of any non-linear audio plugin on the mid or side channel  
//   (use the choosen audio plugins in LR mode !).
//   Adapted from an original idea by Dan Worall (all credits to him !)
//   https://www.youtube.com/watch?v=dD6_Bajj2DI
//   Choose an ID common to a unique encoder/decoder pair, to link both instances together and
//   share the gain values in realtime.
//   Default values are 0dB to implement classical MS encoding.
//   For the settings advised by DW, use +0dB on mid and +6dB on side.
// changelog:
//   v1.0 initial release
//   v1.1 disable encoding or decoding if a plugin with a corresponding ID is not found.
//   v1.2 integrate also a mid encoding gain 

in_pin:left input
in_pin:right input
out_pin:mid output
out_pin:side output

options:gmem=BbhMSEncoderDecoderWithGains

slider1:coder_id=1<1,99,1>Encoder/decoder ID
slider2:decoder_present=0<0,1,1{no,yes}>Decoder present ?
slider3:mid_gain_db=0.0<-32,32,0.01>Mid encoding gain (dB)
slider4:side_gain_db=0.0<-32,32,0.01>Side encoding gain (dB)

//-----------------------------------------------------------------------------------
@init
check_count = 0;
check_max = 10;

//-----------------------------------------------------------------------------------
@slider
gmem[coder_id*5+1] = mid_gain_db;
mid_gain = 10^(mid_gain_db/20);
gmem[coder_id*5+2] = side_gain_db;
side_gain = 10^(side_gain_db/20);

//-----------------------------------------------------------------------------------
@block
gain = 10^(gain_db/20);

(check_count += 1) > check_max ? (
  gmem[coder_id*5] == 1 ? decoder_present = 0;
);
gmem[coder_id*5] != 1 ? (
  gmem[coder_id*5] = 1;
  check_count = 0;
  decoder_present = 1;
);

status = gmem[coder_id*5];

//-----------------------------------------------------------------------------------
@sample
decoder_present == 1 ? (
  mid = (spl0 + spl1) * 0.5 * mid_gain;
  side = (spl0 - spl1) * 0.5 * side_gain;
  spl0 = mid;
  spl1 = side;
);
