desc: MIDI Transpose Project
// version: 1.3
// author: binbinhfr
// about:
//   Transpose MIDI notes of a project by semitones.
//   One instance of this plugin should be added to the master track.
//   Then you have to add a "MIDI Transpose Track" plugin on every MIDI track you want to transpose.
//   This solution makes it easy to transpose a whole MIDI project with one slider.
// changelog:
//   v1.0 initial release
//   v1.1 change scale
//   v1.2 use ext_noinit to avoid project_id change at every play/stop
//   v1.3 add a project transpose octave slider

options:gmem=BbhMidiTransposeProjectAndTracks

slider1:transpose_project=0<-48,48,1>Project transpose (semitones)
slider2:transpose_project_octave=0<-10,10,1>Project transpose (octave)
slider3:transpose_project_status=1<0,1,1{Off,On}>Enable/disable project transpose

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

//-------------------------------------------------------------------------------------
@init
  ext_noinit = 1.0;
  
  version_cur = 1.3;
  
  strcat(#debug,"I");

  //store project transpose value in shared memory (shared between instances)
  // gmem[0] stores next usable project_id
  // gmem[1] stores the id of the last playing project
  // gmem[10+project_id] stores the global transpose to be shared with tracks of the same project
  
  (project_id == 0) ? (gmem[0] += 1; project_id = gmem[0];);
  gmem[1] = project_id; // show active project id.
  gmem[10+project_id] = transpose_project + 12 * transpose_project_octave;
  
//-------------------------------------------------------------------------------------
@serialize

file_avail(0) > -1 ? 
(
  // Loading...
  file_var(0, version);
  version_last = version;
  version < version_cur ? fix_version = 1;
  version = version_cur;  
  
  strcat(#debug,"L");
)
:
(
  block_called ?
  (
    // Saving...
    version == 0 ? fix_version = 1;
    version = version_cur;
    file_var(0, version);
    
    strcat(#debug,"S");
  )
  :
  (
    // Creating...
    strcat(#debug,"C");
  );
);

//-------------------------------------------------------------------------------------
@slider
  gmem[10+project_id] = transpose_project_status ? (transpose_project  + 12 * transpose_project_octave) : 0;

//-------------------------------------------------------------------------------------
@block
  ! block_called ?
  (
    block_called = 1;
    strcat(#debug,"B");
  );
  
  fix_version ?
  (
    fix_version = 0;
    
    version_last < 1.0 ?
    (
      transpose_project_status = transpose_project_octave;
      transpose_project_octave = 0;
    );
    
    strcat(#debug,"F");
  );
  
  // should be called only when the project has focus...
  gmem[1] = project_id; // show active project id.
  
//-------------------------------------------------------------------------------------
@sample
  spl0 = spl0;
  spl1 = spl1;

