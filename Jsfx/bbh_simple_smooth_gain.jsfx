desc: Simple Smooth Gain
// version: 1.2
// author: binbinhfr
// about:
//   A simple plugin that adds or removes gain to the audio signal, 
//   but changing the resulting gain smoothly if the target value changes rapidly.
//   The "smoothness" of the effect can be adjusted
// changelog:
//   v1.0 initial release
//   v1.1 new scale for smoothness
//   v1.2 engaged switch

slider1:engaged=1<0,1,1{off,on}>engaged (on/off)
slider2:gain_db=0<-32,24,0.01>target gain (dB)
slider3:smoothness=100<0,500,1>smoothness (0:immediate,speed:1-500)
slider4:gain_out_db=0<-32,24,0.01>current gain (dB) (read-only)

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init
gain_next=engaged ? (10^(gain_db/20)) : 1.0;
gain_cur=gain_next;
gain_out_db=engaged ? gain_db : 0.0;

@slider
gain_next=engaged?(10^(gain_db/20)):1.0;
time_per_block = samplesblock/srate;
ratio=(smoothness == 0) ? 1 : time_per_block*100/smoothness;

@block
// progressive change
gain_diff = gain_next - gain_cur;

(smoothness != 0 & abs(gain_diff) > 0.0001) ?
(
  gain_delta = gain_diff*ratio;
  gain_cur += gain_delta;
) :
(
  gain_cur = gain_next;
);

(gain_diff != 0) ? 
(
  gain_out_db = 20*(log(gain_cur)/log(10));
  gain_out_db = 0.01*ceil(gain_out_db*100-0.5);
);

@sample
spl0 *= gain_cur;
spl1 *= gain_cur;


