desc:MS Encoder with boosted side
// version: 1.0
// author: binbinhfr 
// about: 
//   Change the gain of the side channel when encoding/decoding, to emphasize/reequilibrate the effect of
//   any MS compliant plugin applied to the signal.
//   Adapted from an original idea of Dan Worall (all credits to him !)
//   https://www.youtube.com/watch?v=dD6_Bajj2DI
//   Choose an ID common to a unique encoder/decoder chain, to link both ionstances together and
//   share the side boost value in realtime.
//   Default side boost value is +6dB (as advised by DW). For classical MS encoding, use 0dB.
// changelog:
//   v1.0 initial release

in_pin:left input
in_pin:right input
out_pin:mid output
out_pin:side output

options:gmem=BbhMSEncoderDecoder

slider1:coder_id=1<1,99,1>Encoder/decoder ID
slider2:gain_db=6.0<-32,32,0.01>Side signal boost (dB)

@slider
gmem[coder_id] = gain_db;
gain = 10^(gain_db/20);

@sample
mid = (spl0 + spl1) * 0.5;
side = (spl0 - spl1) * 0.5 * gain;

spl0 = mid;
spl1 = side;
